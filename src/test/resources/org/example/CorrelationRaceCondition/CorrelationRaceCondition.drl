package org.example.CorrelationRaceCondition;

import java.util.concurrent.ConcurrentSkipListSet
import java.util.Date;

declare Event
  @role(event)
  @timestamp(time)
end

declare DeviceOfflineAffliction
  location : String
end

rule "DeviceAdopted: Update matching affliction by location"
  salience 30
  no-loop true
  when
    $e : Event(eventIdentifier == "DeviceAdopted", location != "", deviceName != "", $location : location, $device : deviceName)
    $a : Event(eventIdentifier == "DeviceOffline", location == $location, deviceName == $device, this before $e)
  then
    delete($a);
  end

rule "DeviceAdopted: Affliction Retracted"
  salience 30
  no-loop true
  when
    $e : Event(eventIdentifier == "DeviceAdopted", location != "", deviceName != "", $location : location)
    not(DeviceOfflineAffliction(location == $location))
  then
    delete($e);
  end

rule "DeviceAdopted: No Matching DeviceOffline Events"
  salience 30
  no-loop true
  when
    $e : Event(eventIdentifier == "DeviceAdopted", location != "", deviceName != "", $location : location,
        $device : deviceName)
    not Event(eventIdentifier == "DeviceOffline", location == $location, deviceName == $device, this before $e)
  then
    delete($e);
  end

rule "DeviceOffline: Create new affliction"
  salience 0
  no-loop true
  when
    $e : Event(eventIdentifier == "DeviceOffline", location != "", deviceName != "", $ei : eventIdentifier,
        $location : location, $device : deviceName)
  then
    insertLogical(new DeviceOfflineAffliction($location));
  end